################################################################################
# VPIC configuration
################################################################################

#------------------------------------------------------------------------------#
# Initialize autoconf
#------------------------------------------------------------------------------#

AC_INIT(vpic, VERSION_TAG, bergen@lanl.gov)
AC_CONFIG_AUX_DIR(config)
AC_CANONICAL_TARGET

VERSION_INFORMATION=LIBRARY_TAG
AC_SUBST(VERSION_INFORMATION)

#------------------------------------------------------------------------------#
# Initialize automake
#------------------------------------------------------------------------------#

AM_INIT_AUTOMAKE

#------------------------------------------------------------------------------#
# Machine file specification
#------------------------------------------------------------------------------#

CONFIG_WITH_MACHINE("$srcdir/machine")

#------------------------------------------------------------------------------#
# Configuration targets
#------------------------------------------------------------------------------#

AC_CONFIG_FILES([Makefile \
	lib/Makefile \
	bin/Makefile \
	bin/vpic \
	test/Makefile \
	test/integrated/Makefile \
	test/integrated/atlocal \
	test/unit/Makefile \
	test/unit/atlocal
])

#------------------------------------------------------------------------------#
# Test sub-directories
#------------------------------------------------------------------------------#

AC_CONFIG_TESTDIR([test/integrated])
AC_CONFIG_TESTDIR([test/unit])

#------------------------------------------------------------------------------#
# Program options
#------------------------------------------------------------------------------#

AC_PROG_CXX
LT_INIT
CONFIG_PROG_DOXYGEN

#------------------------------------------------------------------------------#
# Extension
#------------------------------------------------------------------------------#

EXTENSION=`echo $build | sed 's,-.*,,g'`
AC_SUBST(EXTENSION)

#------------------------------------------------------------------------------#
# MPI
#------------------------------------------------------------------------------#

CONFIG_WITH_MPI(C)
AC_DEFINE(OMPI_SKIP_MPICXX)

EXTRA_CPPFLAGS="$EXTRA_CPPFLAGS $MPI_CPPFLAGS"
EXTRA_LDFLAGS="$EXTRA_LDFLAGS $MPI_LDFLAGS"
EXTRA_LIBS="$EXTRA_LIBS $MPI_LIBS"

#------------------------------------------------------------------------------#
# Set extras
#------------------------------------------------------------------------------#

AC_SUBST(EXTRA_CPPFLAGS)
AC_SUBST(EXTRA_LDFLAGS)
AC_SUBST(EXTRA_LIBS)

#------------------------------------------------------------------------------#
# Config commands
#------------------------------------------------------------------------------#

AC_CONFIG_COMMANDS(links, [make links])
AC_CONFIG_COMMANDS(vpic, [chmod +x bin/vpic])

#------------------------------------------------------------------------------#
# Output
#------------------------------------------------------------------------------#

AC_OUTPUT
